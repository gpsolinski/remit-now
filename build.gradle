plugins {
    id 'java'
    id 'groovy'
    id "io.freefair.lombok" version "4.1.5"
}

group 'com.gpsolinski'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

def vertxVersion = '3.8.4'

dependencies {
    annotationProcessor "io.vertx:vertx-codegen:$vertxVersion:processor"
    annotationProcessor "io.vertx:vertx-web-api-service:$vertxVersion"
    compileOnly "io.vertx:vertx-codegen:$vertxVersion"
    compile "org.codehaus.groovy:groovy-all:2.5.8"
    compile "com.google.guava:guava:23.5-jre"
    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-service-proxy:$vertxVersion"
    compile "io.vertx:vertx-web-api-contract:$vertxVersion"
    compile "io.vertx:vertx-web-api-service:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"
    testCompile "org.spockframework:spock-core:1.3-groovy-2.5"
    testCompile "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1"
}

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources.srcDir 'src/main/resources'
    }
    acceptanceTest {
        groovy.srcDir 'src/acceptanceTest/groovy'
        resources.srcDir 'src/acceptanceTest/resources'
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
    test {
        groovy.srcDir 'src/test/groovy'
        resources.srcDir 'src/test/resources'
    }
}

task acceptanceTest(type: Test, description: 'Runs Acceptance Tests', group: 'verification') {
    testClassesDirs = sourceSets.acceptanceTest.output.classesDirs
    classpath = sourceSets.acceptanceTest.runtimeClasspath
    mustRunAfter test
}

check.dependsOn acceptanceTest

jar {
    manifest {
        attributes 'Main-Class': 'com.gpsolinski.remitnow.web.TransferServiceMainVerticle'
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}